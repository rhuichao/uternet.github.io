---
layout: default
title: Friends and Relations
---

---------------
这是一个set集合吗

`(apple peaches apple plum)`

>不是，apple出现了不止一次

---------------

`(set? lat)` 是真还是假，其中lat是`(apples peaches peaches plums)

>\#t，因为没有重复出现的原子

---------------

那么`(set? lat)`呢，其中lat是`()`

>\#t，因为没有重复出现的原子

---------------

试试看写出函数set?

>```scheme
>(define set?
>  (lambda (lat)
>    (cond
>      ((null? lat) #t)
>      (else (cond
>              ((member? (car lat) (cdr lat)) #f)
>              (else (set? (cdr lat))))))))
>```

---------------

简化set?

>```scheme
>(define set?
>  (lambda (lat)
>    (cond
>      ((null? lat) #t)
>      ((member? (car lat) (cdr lat)) #f)
>      (else (set? (cdr lat))))))
>```

---------------

这个函数对  
`(apple 3 pear 4 9 apple 3 4)`  
有用吗

>是的，member?中现在用equal?替代了eq?

---------------


看到`member?`出现在`set?`的定义中你惊讶吗

>别这样，我们已经写过`member?`了，现在我们想用就用。

---------------

`(makeset lat)`是什么，其中lat是  
`(apple peach pear peach plum apple lemon peach)`

>`(apple peach pear plum lemon)`

---------------


试试用member?写出函数makeset

>```scheme
>(define makeset
>  (lambda (lat)
>    (cond
>      ((null? lat) (quote ()))
>      ((member? (car lat) (cdr lat))
>       (makeset (cdr lat)))
>      (else (cons (car lat)
>                   (makeset (cdr lat)))))))
>```

---------------

函数短得让你惊讶吧

>希望如此。别怕：这个是正确的。

---------------

使用之前的定义，那么(makeset lat)是什么，其中   
(apple peach pear peach plum apple lemon peach)

>(pear plum apple lemon peach)

---------------

试试用 multirember 重写 makeset 函数

>```scheme
>(define makeset
>  (lambda (lat)
>    (cond
>     ((null? lat) (quote ()))
>     (else (cons (car lat)
>                 (makeset
>                  (multirember (car lat)
>                               (cdr lat))))))))
>```

---------------

使用第二个定义，那么(makeset lat)是什么，其中lat是   
`(apple peach pear peach plum apple lemon peach)`

>`(pear plum apple lemon peach)`

---------------

用自己的话描述及喜爱第二个 makeset 函数定义是如何工作的。

>下面是我们的描述：   
>“函数 makeset 在用删除再次出现与第一个原子相同的原子成员后，把第一个原子cons到自然递归上。”

---------------

这个函数对  
`(apple 3 pear 4 9 apple 3 4)`  
有用吗

>是的，multirember中现在用equal?替代了eq?

---------------

`(subset? set1 set2)`  
是什么，其中   
set1 是`(5 chicken wings)`   
set2 是`(5 hamburgers 2 pieces fried chicken and light duckling wings)`

>\#t，因为每一个set1中的原子也在set2中。

---------------

写出函数 subset?

>```scheme
>(define subset?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #t)
>      (else (cond
>              ((member? (car set1) set2)
>               (subset? (cdr set1) set2))
>              (else #f))))))
>```

---------------

你能写出一个更简单的吗

>```scheme
>(define subset?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #t)
>      ((member? (car set1) set2)
>       (subset? (cdr set1) set2))
>      (else #f))))
>```

---------------

试试用(and ...)重写 subset?

>```scheme
>(define subset?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #t)
>      (and ((member? (car set1) set2)
>            (subset? (cdr set1) set2))))))
>```

---------------

`(eqset? set1 set2)`   
是什么，其中   
set1 是`(6 large chickens with wings)`，   
set2 是`(6 chickens with large wings)`

>\#t

---------------

写出函数eqset?

>```scheme
>(define eqset?
>  (lambda (set1 set2)
>    (cond
>      ((subset? set1 set2)
>       (subset? set1 set2))
>      (else #f))))
>```

---------------

你能用一行的cond-写出 eqset吗

>```scheme
>(defin eqset?
>  (lambda (set1 set2)
>    (cond
>     (else (and (subset? set1 set2)
>                (subset? set2 set1))))))
>```

---------------

写出一行版

>```scheme
>(define eqset?
>  (lambda (set1 set2)
>    (and (subset? set1 set2) 
>         (subset? set1 set2))))
>```

---------------

`(intersect? set1 set2)`  
是什么，其中   
set1 是 `(stewed tomatoes and macaroni)`   
set2 是 `(macaroni and cheese)`

>\#t，因为set1至少有一个原子出现在set2中。交集

---------------

定义函数 intersect?

>```scheme
>(define intersect?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #f)
>      (else (cond
>              ((member? (car set1)  set2) #t)
>              (else (intersect? (cdr set1) set2)))))))
>```

---------------

写出更简洁的版本

>```scheme
>(define intersect?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #f)
>      ((member? (car set1) set2) #t)
>      (else (intersect? (cdr set1) set2)))))
>```

---------------

试试使用(or ...)写 intersect?函数

>```scheme
>(define intersect?
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) #f)
>      (else (or (member? (car set1)  set2)
>                (intersect? (cdr set1) set2))))))
>```

---------------

对比`subset?` 和`interset?`

`(intersect set1 set2)`是什么，其中  
set1 是`(stewed tomatoes and macaroni)`  
set2 是`(macaroni and cheese)`

>`(and macaroni)`

---------------


现在你能写出简短版的函数 intersect

>```scheme
>(define intersect
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) (quote ()))
>      ((member? (car set1) set2)
>       (cons (car set1)
>             (intersect (cdr set1) set2)))
>      (else (intersect (cdr set1) set2)))))
>```

---------------

`(union set1 set2)`   
是什么，其中   
set1 是`(stewed tomatoes and macaroni casserole)`   
set2 是`(macaroni and cheese)`

>`(stewed tomatoes casserole macaroni and cheese)`

---------------

写出函数union

>```scheme
>(define union
>  (lambda (set1 set2)
>    (cond
>      ((null? set1) set2)
>      ((member? (car set1) set2)
>       (union (cdr set1) set2))
>      (else (cons (car set1)
>                  (union (cdr set1) set2))))))
>```

---------------

这个函数是什么

```scheme
(define xxx
  (lambda (set1 set2)
    (cond
     ((null? set1) '())
     ((member? (car set1) set2)
      (xxx (cdr set1) set2))
     (else
      (cons (car set1)
            (xxx (cdr set1) set2))))))
```

>我们的解释是：   
>“这个函数返回的是set1中有，但是set2中没有的原子。

---------------

`(intersectall l-set)`   
是什么，其中   
l-set 是 `((a b c) (c a d e) (e f g h a b))`

>(a)  
>它返回在l-set的所有子set中都有的原子。

---------------


`(intersectall l-set)`   
是什么，其中 l-set 是

    ((6 pears and) 
     (3 peaches and 6 peepers)
     (8 pears and 6 plums)
     (and 6 prunes with some apples))

>`(6 and)`
>6 和 and 在上面4个列表中都出现了

---------------

现在使用任何你需要的的辅助函数写出 intersectall ，假定所有的集合表都非空。

>```scheme
>(define intersectall
>  (lambda (l-set)
>    (cond
>     ((null? (cdr l-set)) (car l-set))
>     (else
>      (intersect (car l-set)
>                 (intersectall (cdr l-set)))))))
>```

---------------

这是一对 pair吗  
`(pear pear)`

>是的，列表中只有两个原子。

---------------

这是一对 pair吗  
`(3 7)`

>是

---------------

这是一对 pair吗  
`((2) (pair))`

>是。 因为列表中只有两个表达式。

---------------

`(a-pair? l)`  
其中 l 是  
`(full (house))`

>\#t。 因为列表中只有两个表达式。


---------------

定义函数 a-pair?

>```scheme
>(define a-pair?
>  (lambda (x)
>    (cond
>      ((atom? x) #f)
>      ((null? x) #f)
>      ((null? (cdr x)) #f)
>      ((null? (cdr (cdr x))) #t)
>      (else #f))))
>```

---------------

怎样取得一个piar的第一个 S-expression表达式

>对pair取car

---------------

怎样取得一个piar的第二个 S-expression表达式

>对pair取cdr再取car

---------------

怎样用两个原子构建一个 pair

>把两个原子依次cons到一个`()`里，即`(cons x1 (cons x2 (quote ())))`

---------------

怎样用两个S-expression表达式构建一个 pair

>把两个S-expression表达式依次cons到一个`()`里，即`(cons x1 (cons x2 (quote ())))`

---------------

觉得上边最后两个问题的回答有什么区别吗

>没什么区别

---------------

```scheme
(define first
  (lambda (p)
    (cond
      (else (car p)))))

(define second
  (lambda (p)
    (cond
      (else (car (cdr p))))))

(define build
  (lambda (s1 s2)
    (cond
      (else (cons s1 (cons s2 (quote ())))))))
```

这三个函数会有什么用

>它们被用来表示pair对和获得pair对的成员。详见第六章。他们用来提高可读性，见下文。

---------------

请把这三个函数用一行重定义。

```scheme
(define first
  (lambda (p)
    (car p)))

(define second
  (lambda (p)
    (car (cdr p))))

(define build
  (lambda (s1 s2)
    (cons s1 (cons s2 (quote ())))))
```

---------------

你能写出third的一行定义吗

>```scheme
>(define third
>  (lambda (p)
>    (car (cdr (cdr p)))))
>```

---------------

l 是 rel 吗，其中，l 是   
`(apple peaches pumpkin pie)`

>不是，因为 l 不是全由 pair 对构成。我们使用 rel 来表示relation。

---------------

l`是 rel 吗，其中，l`是   
`((apples peaches) (pumpkin pie) (apples peaches))`

>不是，因为 l 不是pair对的一个的集合，有重复的。

---------------

l 是 rel 吗，其中，l 是   
`((apples peaches) (pumpkin pie) )`

>是

---------------

l 是 rel 吗，其中，l 是   
`((4 3) (4 2) (7 6) (6 2) (3 4))`

>是

---------------

rel 是 fun 吗，其中 rel 是   
`((4 3) (4 2) (7 6) (6 2) (3 4))`

>不是，我们用fun表示function函数。

---------------

`(fun? rel)`   
的值是什么，其中rel是   
`((8 3) (4 2) (7 6) (6 2) (3 4))`

>\#t，因为`(firsts rel)`是一个 set。 详见第三章。
>firsts取出各子列表的第一个元素：  
>`(8 4 7 6 3)`  
>里边没有重复的原子

---------------

`(fun? rel)`   
是什么，其中rel是   
((d 4) (b 0) (b 9) (e 5) (g 4))

>\#f，因为 b 重复了。

---------------

用 set? 和 firsts 写出函数fun?

>```scheme
>(define fun?
>  (lambda (rel)
>    (set? (firsts rel))))
>```

---------------

函数fun?是简单的one-liner吗

>当然是啊

---------------


我们怎么定义有限函数

>对于我们，有限函数是pair对组成的list表，其中每个pair的第一个元素没有相同的。

---------------

`(revrel rel)`  
是什么，其中 l 是   
`(8 a) (pumpkin pie) (got sick))`

>`((a 8) (pie pumpkin) (sick got))`

---------------

现在能写出revrel函数

>```scheme
>(define revrel
>  (lambda (rel)
>    (cond
>     ((null? rel) '())
>     (else
>      (cons (build
>             (second (car rel))
>             (first (car rel)))
>            (revrel (cdr rel)))))))
>```

---------------

下面的这个对吗

```scheme
(define revrel
  (lambda (rel)
    (cond
      ((null? rel) (quote ()))
      (else (cons (cons (car (cdr (car rel)))
                        (cons (car (car rel))
                              (quote ())))
                  (revrel (cdr rel)))))))
```

>是的，现在你看出“representation表达”对可读性的益处了吧。

---------------

假设我们有如下函数revpair可以反转一个piar对的两个成员。

```scheme
(define revpair
  (lambda (pair)
    (build (second pair) (first pair))))
```

我们如何用这个辅助函数重写revrel

>没问题，而且更易读
>
>```scheme
>(define revrel
>  (lambda (rel)
>    (cond
>      ((null? rel) (quote ()))
>      (else (cons (revpair (car rel))
>                  (revrel (cdr rel)))))))
>```

---------------

猜猜看为什么fun不是fullfun，其中fun是   
`((8 3) (4 2) (7 6) (6 2) (3 4))`

>fun不是fullfun，因为所有pair的第二项中2出现了不止一次。

---------------

为什么`(fullfun? fun)`是 #t，其中fun是   
`((8 3) (4 8) (7 6) (6 2) (3 4))`

>因为`(3 8 6 2 4)`是一个set。

---------------

`(fullfun? fun)`  
是什么，其中fun是  
`((grape raisin) (plum prune) (stewed prune))`

>\#f

---------------

`(fullfun? fun)`   
是什么，其中fun是   
`((grape raisin) (plum prune) (stewed grape))`

>\#t，因为`(raisin prune grape)`是一个集合。

---------------

定义fullfun?

>```scheme
>(define fullfun?
>  (lambda (fun)
>    (set? (seconds fun))))
>```

---------------

你能定义seconds吗

>如同firsts

---------------

fullfun?还可以称作什么

>one-to-one一对一

---------------

你能想出第二种one-to-one的写法吗

>```scheme
>(define one-to-one?
>  (lambda (fun)
>    (fun? (revrel fun)))
>```

---------------

问  
`((chocolate chip) (doughy cookie))`  
是一个 one-to-one一对一函数吗?

>是的，你现在就该来一份！

---------------

<center>去点一份((chocolate chip) (doughy cookie))吧！</center>


<center>或者你自己做一份，这样不是更好吗</center>

---------------

```scheme
(define cookies
  (lambda ()
    (bake
     (quote (350 degrees))
     (quote (12 minutes))
     (mix
      (quote (walnuts 1 cup))
      (quote (chocolate-chips 16 ounces))
      (mix
       (mix
        (quote (flour 2 cups))
        (quote (oatmeal 2 cups))
        (quote (salt 5 teaspoon))
        (quote (baking-powder 1 teaspoon))
        (quote (baking-soda 1 teaspoon)))
       (mix
        (quote (eggs 2 large))
        (quote (vanilla 1 teaspoon))
        (cream
         (quote (butter 1 cup))
         (quote (sugar 2 cups)))))))))
```
